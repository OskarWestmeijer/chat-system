plugins {
    id 'java'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group = 'westmeijer.oskar'
    version = '1.0'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation 'ch.qos.logback:logback-classic:1.5.18'

        compileOnly("org.projectlombok:lombok:1.18.38")
        annotationProcessor("org.projectlombok:lombok:1.18.38")

        testCompileOnly("org.projectlombok:lombok:1.18.38")
        testAnnotationProcessor("org.projectlombok:lombok:1.18.38")

        testImplementation(platform('org.junit:junit-bom:5.13.1'))
        testImplementation('org.junit.jupiter:junit-jupiter')
        testRuntimeOnly('org.junit.platform:junit-platform-launcher')
        testImplementation 'org.assertj:assertj-core:3.27.3'
        testImplementation 'org.mockito:mockito-core:5.17.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.17.0'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required.set(true)
            csv.required.set(false)
            html.required.set(true)
        }
    }
}

tasks.register('buildJars') {
    dependsOn ':client:shadowJar', ':server:shadowJar'
}

tasks.named('check') {
    dependsOn tasks.named('buildJars')
}
